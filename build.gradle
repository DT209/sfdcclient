group 'com.github.dt209'
version '0.1'

apply plugin: 'java'
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.apache.commons:commons-text:1.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                name 'Simple Salesforce Client'
                packaging 'jar'
                description 'Simple Salesforce Client that allows for CRUD operations on Salesforce objects'
                url 'https://github.com/DT209/sfdcclient/'

                scm {
                    url 'https://github.com/DT209/sfdcclient/tree/maste'
                    connection 'scm:https://github.com/DT209/sfdcclient.git'
                    developerConnection 'scm:git://github.com/DT209/sfdcclient.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'dt209'
                        name 'David T'
                        email 'david.javaarchitect@gmail.com'
                    }
                }
            }
        }
    }
}